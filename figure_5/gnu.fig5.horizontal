#set terminal postscript enhanced
#set terminal postscript portrait color "Times-Roman" 12
#set terminal postscript eps enhanced color "Arial" 12 size 8.8cm,9.6cm clip
set terminal postscript eps enhanced color "Arial" 12 size 8.8cm,8.8cm clip
set output "fig5.horizontal.eps"

set size 1,1

set notitle
set nokey

set multiplot

#set pm3d depthorder hidden3d 1
#set hidden3d


#BIPARENTAL: MAP (E0,w)
set origin 0, 0.5
set size   0.5, 0.49
set title  "(a) Biparental" offset 0,0  font ',14'
#set label  "I"   offset 3,2  font ',18'
#set label  "II"  offset 6,10 font ',18'
#set label  "III" offset 15,6 font ',18'
set label  "host bimorphic"        offset 1.5,7  font ',12' rotate by -70
set label  " host \n extinction"   offset 1,13.0 font ',12'
set label  "fixation of\nsymbiont" offset 13,9   font ',12'
set xlabel "{/Times-Italic w}" font      ',14'
#set xrange [0.05:1.26]	 
set xrange [0.0:1.26]	 
set xtics  0, 0.5, 1.25 offset 0,0
set mxtics 2
set xtics  mirror
set ylabel "{/Times-Italic e}_0" offset 2,0 font ',14'
set yrange [0:1.25]
set ytics  0, 0.5, 1.25 offset 0,0
set mytics 2
set ytics  mirror

plot\
"01_biparental/map/lineAB.dat" using 2:1 with lines  smooth csplines lt 1 lw 2.0 lc rgb 'red'  axis x1y1 ,\
"01_biparental/map/lineAC.dat" using 1:2 with lines  lt 1 lw 2.0 lc rgb 'red'  axis x1y1 ,\
"01_biparental/map/point.dat"  using 1:2 with points pt 9 ps 1.2 lc rgb 'blue' axis x1y1


set nolabel


#MATERNAL MAP (E0,w)
set origin 0.5, 0.5
set size   0.5, 0.49
set title  "(b) Maternal" offset 0,0
#set label  "I"   offset 6,5    font ',18'
#set label  "II"  offset 4,13   font ',18'
#set label  "III" offset 15,9   font ',18'
#set label  "bimorphic"  offset 4,5    font ',14'
#set label  "extinction" offset 1,13   font ',14'
#set label  "fixation"   offset 14,9   font ',14'
set label  "host bimorphic"        offset 3,5    font ',12'
#set label  " host \n extinction"   offset 1,14.3 font ',12'
set label  " host \n extinction"   offset 1,13.0 font ',12'
set label  "fixation of\nsymbiont" offset 14,9   font ',12'
plot\
"02_maternal/map/lineAB.dat" using 1:2     with lines lt 1 lw 2 lc rgb 'red' ,\
"02_maternal/map/lineAC.dat" using 1:2     with lines lt 1 lw 2 lc rgb 'red' ,\
"02_maternal/map/lineBC.dat" using 1:2     with lines lt 1 lw 2 lc rgb 'red' ,\
"01_biparental/map/point.dat"  using 1:2 with points pt 9 ps 1.2 lc rgb 'blue' axis x1y1

set nolabel
set notitle

#set key	at 50,1630 samplen 1.5
set key		at 45,1630 samplen 1.5
#set nokey


#SYMBIONT START NEAR EQUILBRIUM
set origin      0.1, 0.0
set size        0.9, 0.45
set label       "(c)" at 40,1900 font ',14'
set xlabel     "time"
set xrange      [0.0:90.0]
set xtics       0, 20, 80
set mxtics      2
set xtics       nomirror
set ylabel      "host population size" offset 0,0
set yrange      [0.0:1700.0]
set ytics       0,500,6000
set mytics      2
set ytics       nomirror
set y2label     "frequency" offset -2,0
set y2range     [0:1.2]
set y2tics      0,0.5,1
set my2tics     5
set ticslevel   0
plot \
"01_biparental/simulate_w_0.37_E0_0.1/output.timeseries.dat"  using 1:5  title ' number of hosts'           with lines lt 1 lw 1 lc rgb 'purple' axis x1y1 ,\
"01_biparental/simulate_w_0.37_E0_0.1/output.timeseries.dat"  using 1:8  title ' freq. of community {/Times-Italic S}' with lines lt 4 lw 1 lc rgb 'green'  axis x1y2 ,\
"01_biparental/simulate_w_0.37_E0_0.1/output.timeseries.dat"  using 1:11 title " freq. of {/Times-Italic M^{/normal=10 {-}}}"   with lines lt 4 lw 1 lc rgb 'blue'   axis x1y2 ,\
"03_biparental_ODE/output.timeseries.dat" using 1:2  title '  differential eqs' with lines lt 1 lw 1 lc rgb 'red' axis x1y1 ,\
"03_biparental_ODE/output.timeseries.dat" using 1:3  notitle  with lines lt 4 lw 1 lc rgb 'red' axis x1y2 ,\
"03_biparental_ODE/output.timeseries.dat" using 1:4  notitle  with lines lt 4 lw 1 lc rgb 'red' axis x1y2

# 'notitle' in the two lines above removes these lines from the key


set notitle
set noxlabel
set noylabel
set nosize
set border 


set nomultiplot
